plugins {
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
  	id "com.github.node-gradle.node" version "2.2.4"
}

group = 'com.github.pierregoudjo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

node {
	download = true
	version = "14.7.0"
}

task installNpmPackages(type: NpmTask) {
	workingDir = file("${project.projectDir}/src/main/webapp")
	args = ["install"]
}

task buildFrontend(type: NpmTask) {
	workingDir = file("${project.projectDir}/src/main/webapp")
	args = ["run", "build"]
}

task copyWebApp(type: Copy) {
	from 'src/main/webapp/build'
	into 'build/resources/main/static/.'
}

buildFrontend.dependsOn installNpmPackages
copyWebApp.dependsOn buildFrontend
compileJava.dependsOn copyWebApp